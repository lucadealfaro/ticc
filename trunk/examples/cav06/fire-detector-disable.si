module ControlUnit:
    var s: [0..3] // 0=waiting, 1=alarm raised, 2=fd called, 3=disabled
    initial: s = 0
    input fire:     { local: s = 0 | s = 1 ==> s' := 1 
                       else  s = 2         ==>         } 
    input disable:  { local: true ==> s' := 3 }
    output call_fd: { s = 1 ==> s' = 2 }
endmodule

module FireDetector1:
    var s: [0..2] // 0=idle, 1=smoke detected, 2=inactive or alarm raised
    initial: s = 0
    input smoke1:  { local: s = 0 | s = 1 ==> s' := 1 
                     else   s = 2 ==>        } // do nothing if inactive 
    output fire:   { s = 1        ==> s'  = 2 }
    input fire:    { } // other modules can detect fire too       
    input disable: { local: true  ==> s' := 2 }
endmodule

module Faulty_FireDetector1: 
    var s: [0..2] // 0=idle, 1=smoke detected, 2=alarm raised
    initial: s = 0
    input smoke1:  { local: s = 0 | s = 1 ==> s' := 1 
                     else   s = 2 ==>        } // do nothing if inactive 
    output fire:   { s = 1        ==> s'  = 2 }
    input fire:    { } // other modules can detect fire too       
    // does not listen to disable action 
endmodule

module FireDetector2:
    var s: [0..2] // 0=idle, 1=smoke detected, 2=inactive or alarm raised
    initial: s = 0
    input smoke2:  { local: s = 0 | s = 1 ==> s' := 1
                     else  s = 2 ==>         } // do nothing if inactive 
    output fire:   { s = 1        ==> s'  = 2 }
    input fire:    { } // other modules can detect fire too       
    input disable: { local: true  ==> s' := 2 }
endmodule

module Disable: 
    output disable: { true ==> true } // Can disable at will 
    output smoke1:  { true ==> true }
    output smoke2:  { true ==> true }
endmodule 
